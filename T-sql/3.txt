
/*

======================= метод оптимизационной математики   ===================

Основан на оптимизационной математики (метод исчерпывающего поиска),
это алгоритм для поиска минимума ошибки путём последовательного перебора значений
в заданном диапазоне с фиксированным шагом (поиском наилучшего решения (минимума или максимума) целевой функции).


*/

ALTER PROCEDURE CalcPrices
    @InputPriceWithNDS DECIMAL(10,2),
    @ProcNDS DECIMAL(5,2),
    @CorrectedPriceWithNDS DECIMAL(10,2) OUTPUT,
    @CorrectedPriceWithoutNDS DECIMAL(10,2) OUTPUT,
    @Iterations INT OUTPUT
AS
BEGIN
    DECLARE @k DECIMAL(18,6) = 1 + @ProcNDS / 100.0;
    DECLARE @basePrice DECIMAL(18,6);
    DECLARE @left DECIMAL(18,6);
    DECLARE @right DECIMAL(18,6);
    DECLARE @mid DECIMAL(18,6);
    DECLARE @w_full DECIMAL(18,6);
    DECLARE @w DECIMAL(18,6);
    DECLARE @offset DECIMAL(18,6) = 0.03; -- параметр который нужно подбирать
    DECLARE @iteration INT = 0;
    DECLARE @maxIterations INT = 1000;
    DECLARE @bestWithout DECIMAL(18,6) = NULL;
    DECLARE @bestWith DECIMAL(18,6) = NULL;
    DECLARE @minError DECIMAL(18,6) = 999999.99;

    -- Обработка крайних случаев
    IF @ProcNDS < 0 OR @ProcNDS >= 100 OR @InputPriceWithNDS < 0
    BEGIN
        SET @CorrectedPriceWithoutNDS = 0.00;
        SET @CorrectedPriceWithNDS = 0.00;
        SET @Iterations = 0;
        RETURN;
    END
    IF @ProcNDS = 0
    BEGIN
        SET @CorrectedPriceWithoutNDS = @InputPriceWithNDS;
        SET @CorrectedPriceWithNDS = @InputPriceWithNDS;
        SET @Iterations = 0;
        RETURN;
    END
    IF @InputPriceWithNDS = 0
    BEGIN
        SET @CorrectedPriceWithoutNDS = 0.00;
        SET @CorrectedPriceWithNDS = 0.00;
        SET @Iterations = 0;
        RETURN;
    END

    SET @basePrice = ROUND(@InputPriceWithNDS / @k, 2);
    IF @InputPriceWithNDS > 100.00
        SET @offset = 0.11; -- параметр который нужно подбирать
    SET @left = @basePrice - @offset;
    SET @right = @basePrice + @offset;
    IF @left < 0 SET @left = 0.00;

    SET @mid = @left;
    WHILE @mid <= @right AND @iteration < @maxIterations
    BEGIN
        SET @iteration = @iteration + 1;
        SET @w_full = @mid * @k;
        IF @w_full = ROUND(@w_full, 2)
        BEGIN
            SET @w = ROUND(@w_full, 2);
            DECLARE @error DECIMAL(10,2) = ABS(@w - @InputPriceWithNDS);
            IF @error < @minError
            BEGIN
                SET @minError = @error;
                SET @bestWithout = @mid;
                SET @bestWith = @w;
            END
        END
        SET @mid = @mid + 0.01;
    END

    IF @bestWithout IS NULL
    BEGIN
        SET @bestWithout = @basePrice;
        SET @bestWith = ROUND(@basePrice * @k, 2);
    END

    SET @CorrectedPriceWithoutNDS = @bestWithout;
    SET @CorrectedPriceWithNDS = @bestWith;
    SET @Iterations = @iteration;
END
GO

-- Создание тестовой процедуры
ALTER PROCEDURE TestCalcPrices
AS
BEGIN
    DECLARE @testCases TABLE (
        InputPriceWithNDS DECIMAL(10,2),
        ProcNDS DECIMAL(5,2),
        ExpectedCorrectedPriceWithNDS DECIMAL(10,2),
        ExpectedCorrectedPriceWithoutNDS DECIMAL(10,2)
    );

    INSERT INTO @testCases VALUES
        (1.81, 20, 1.80, 1.50),
        (1.81, 18, 1.77, 1.50), 
        (0.00, 20, 0.00, 0.00),
        (50.00, 0, 50.00, 50.00),
        (100.00, 999, 0.00, 0.00),
        (-10.00, 20, 0.00, 0.00),
        (10.00, -5, 0.00, 0.00),
        (10.00, 108, 0.00, 0.00),
        (1266773.46, 22, 1266773.46, 1038333.82);

    DECLARE @InputPriceWithNDS DECIMAL(10,2), @ProcNDS DECIMAL(5,2),
            @CorrectedPriceWithNDS DECIMAL(10,2), @CorrectedPriceWithoutNDS DECIMAL(10,2),
            @Iterations INT;

    DECLARE test_cursor CURSOR FOR
        SELECT InputPriceWithNDS, ProcNDS FROM @testCases;

    OPEN test_cursor;
    FETCH NEXT FROM test_cursor INTO @InputPriceWithNDS, @ProcNDS;

    WHILE @@FETCH_STATUS = 0
    BEGIN
        BEGIN TRY
            EXEC CalcPrices @InputPriceWithNDS, @ProcNDS, @CorrectedPriceWithNDS OUTPUT, @CorrectedPriceWithoutNDS OUTPUT, @Iterations OUTPUT;
            PRINT 'Input: ' + CAST(@InputPriceWithNDS AS VARCHAR) + ', NDS%: ' + CAST(@ProcNDS AS VARCHAR) +
                  ' -> WithNDS: ' + CAST(@CorrectedPriceWithNDS AS VARCHAR) +
                  ', WithoutNDS: ' + CAST(@CorrectedPriceWithoutNDS AS VARCHAR) +
                  ', Iterations: ' + CAST(@Iterations AS VARCHAR);
        END TRY
        BEGIN CATCH
            PRINT 'Error for Input: ' + CAST(@InputPriceWithNDS AS VARCHAR) + ', NDS%: ' + CAST(@ProcNDS AS VARCHAR) +
                  ' - ' + ERROR_MESSAGE();
        END CATCH

        FETCH NEXT FROM test_cursor INTO @InputPriceWithNDS, @ProcNDS;
    END

    CLOSE test_cursor;
    DEALLOCATE test_cursor;
END
GO

EXEC TestCalcPrices;
